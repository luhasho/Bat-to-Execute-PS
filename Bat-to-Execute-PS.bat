@echo off
rem ##########################################################
rem 本バッチファイルと同名のps1 を実行するバッチです
rem ##########################################################

rem ----------------------------------------------------------
rem 参考1
rem ----------------------------------------------------------
rem ファイル名(拡張子を除く): %~n0

rem ----------------------------------------------------------
rem 参考2
rem ----------------------------------------------------------
rem -ExecutionPolicy について
rem Unrestricted:リモート許可
rem RemoteSigned:ローカル許可（署名付きはリモートも許可）

:: PSファイル名をセットします
set Ps_File_Name=.\%~n0.ps1

:: 自分自身のファイルパスへ移動する(管理者権限だとc:\windows\system32\に移動してまうため)
cd /d %~dp0

rem ##########################################################
rem 管理者特権で実行しないとエラーになる処理
rem ##########################################################
net session >nul 2>&1
rem net openfiles >nul 2>&1

rem openfiles   :管理者権限でないエラーになるコマンド(WOW64対応だとNG)
rem net session :管理者権限でないエラーになるコマンド(WOW64対応でもOK)
rem >nul        :標準出力をnulへ
rem 2>&1        :標準エラー出力を &1 へ
rem &1          :通常の出力へ(この場合は、nulが通常出力になっている)

:: 上記処理がエラー（ユーザー権限）であればユーザー権限で実行する処理に移動
if not %errorlevel% EQU 0 goto User_Auth_Label

rem ##########################################################
rem 管理者権限で実行する処理
rem ##########################################################
echo ######################################################################
echo %Ps_File_Name% を実行します。
echo ######################################################################
::本バッチと同名の ps 実行します
powershell -NoProfile -ExecutionPolicy RemoteSigned %Ps_File_Name%
rem powershell -verb runas -NoProfile -ExecutionPolicy RemoteSigned %Ps_File_Name%

pause

:: 下記の記述で管理者権限の時に終了させる
goto end


rem ##########################################################
rem ユーザー権限で実行する処理
rem ##########################################################
:User_Auth_Label
:: 管理者権限で自分自身を実行（昇格処理）
powershell start-process %~nx0 -verb runas


rem ##########################################################
rem endラベル
rem ##########################################################
:end


